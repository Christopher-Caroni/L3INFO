set LEGUMES circular;
/* Les légumes suivent un cycle */

set PARCELLES;
/* Les différentes parcelles de l'exploitation de Bertrand. */

param N_ANNNES = 6;
/* Le nombre N_ANNNES d'années que l'on souhaite traiter. */

param kilo_engrais_bonus_dispo = 500;

set ANNEE := 1 .. N_ANNNES;
/* Les différentes années dépendantes du paramètre N_ANNNES */

param rendement_tonne_parcelle_an {LEGUMES};
/* le rendement de chaque LEGUMES en tonne/PARCELLES/an */

param prix_vente_euro_kg {LEGUMES};
/* le prix de vente de chaque LEGUMES en euro/kg */

param apport_richesse_an {LEGUMES};
/* l'apport en richesse du sol de chaque LEGUMES par an */

param richesse_initiale {PARCELLES};
/* la richesse initiale de chaque PARCELLES au début de la première année. */

var kilos_engrais_bonus {PARCELLES} >= 0;
/* Les engrais ne sont utilisés que la première année en plus des légumes */

var minimum_richesse_parcelle;
/* Richesse la plus basse parmis toutes les parcelles */

var est_affecte {LEGUMES, PARCELLES, ANNEE} binary;
/*
est_affecte[l,p,a]=1 si l est cultivé sur la PARCELLES p lors de l'année a et 0 sinon.
*/

var richesse_parcelle_annee {PARCELLES, ANNEE};
/* Richesse des différentes parcelles par année. */

var max_richesse {a in ANNEE} = sum {p in PARCELLES} richesse_parcelle_annee[p,a];
/* La richesse de toutes les parcelles par années. */


maximize minimum_richesse_parcelle_sixième_annee :
	 minimum_richesse_parcelle;
/* Maximiser la richesse la plus basse à la 6ième année */

subject to engrais_max_premiere_annee :
	sum { p in PARCELLES } kilos_engrais_bonus[p] <= kilo_engrais_bonus_dispo;
/* On ne dépasse pas 500 kilos d'engrais lors de la première année */

subject to calcul_minimum_richesse_annee_six {p in PARCELLES}:
	minimum_richesse_parcelle <= richesse_parcelle_annee[p, 6];
/* Calcul de la richesse la plus basse lors de la sixième année. */

subject to calcul_richesse_parcelle_avec_engrais { p in PARCELLES } :
	richesse_parcelle_annee[p,1] =
				     sum {l in LEGUMES} (apport_richesse_an[l] * est_affecte[l,p,1]) + kilos_engrais_bonus[p] * 0.01
				     + richesse_initiale[p];
/* Calcul de la richesse des parcelles lors de la première année (avec utilisation d'engrais). */

subject to calcul_richesse_parcelle_sans_engrais { p in PARCELLES, a in 2..N_ANNNES } :
	richesse_parcelle_annee[p,a] =
				     sum {l in LEGUMES} (apport_richesse_an[l] * est_affecte[l,p,a]) + richesse_parcelle_annee[p, a-1];
/* Calcul de la richesse des parcelles lors des autres années (sans utilisation d'engrais). */

subject to richesse_positive {p in PARCELLES, a in ANNEE} :
	richesse_parcelle_annee[p, a] >= 0;
/* On verifie que la richesse de la PARCELLES est positive */

subject to un_legume_par_parcelle {p in PARCELLES, a in ANNEE} :
	sum{l in LEGUMES} est_affecte[l,p,a] = 1;
/* On s'assure qu'un seul LEGUMES est planté par PARCELLES lors d'une même année. */

subject to chaque_type_legume_utilise_au_moins_une_fois {l in LEGUMES, a in ANNEE} :
	sum {p in PARCELLES} est_affecte[l,p,a] >= 1;
/*Chaque type de légume doit être cultivé au moins une fois par année. */

subject to peut_cultiver_legume_premiere_annee {p in PARCELLES, l in LEGUMES} :
	richesse_initiale[p] + apport_richesse_an[l] * est_affecte[l,p,1] + kilos_engrais_bonus[p]*0.01 >= 0;
/* Verifie, pour chaque LEGUMES et chaque PARCELLES, s'il est possible de les cultiver lors de la première année */

subject to peut_cultiver_legume_autres_annees {p in PARCELLES, l in LEGUMES, a in 2..N_ANNNES} :
	est_affecte[next(l),p,a] = est_affecte[l,p,a-1];
/* Verifie, pour chaque LEGUMES et chaque PARCELLES, s'il esy possible de les cultiver lors des autres années */

data;

set LEGUMES := fruits feuilles racines gousses engrais_verts;
set PARCELLES := A B C D E F G;

param :     rendement_tonne_parcelle_an   prix_vente_euro_kg     apport_richesse_an   :=
fruit                2.7                          4.3                   0
feuille              2.6                          1.2                   0
racine               3.4                          2.7                   0
gousse               0.5                          3.3                 3.5
engrais                0                            0                25.8;

param : richesse_initiale :=
A						43.2
B						12.1
C						20.8
D						16.8
E						5.7
F						16.3
G						15.1;
