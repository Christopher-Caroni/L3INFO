
/*
  Les légumes suivent un cycle
*/
set LEGUME circular;

/*
  Les différentes parcelles de l'exploitation de Bertrand.
*/
set PARCELLE;

/*
  Le nombre N_ANNEES d'années que l'on souhaite traiter.
*/
param N_ANNEES = 6;

/*
  La quantité totale d'engrais bonus disponible
*/
param KILO_ENGRRAIS_BONUS_DISPO = 500;

/*
  Les différentes années dépendantes du paramètre N_ANNEES
*/
set ANNEE := 1 .. N_ANNEES;

/*
  le rendement de chaque legume en tonne/parcelle/an
*/
param rendement_tonne_parcelle_an {LEGUME};

/*
  le prix de vente de chaque legume en euro/kg
*/
param prix_vente_euro_kg {LEGUME};

/*
  - apport_besoin_net_an[l] > 0: l'apport en richesse du légume l pour un an
  - apport_besoin_net_an[l] < 0: Le besoin en richese du légume l pour un an
*/
param apport_besoin_net_an {LEGUME};

/*
  la richesse initiale de chaque parcelle au début de la première année.
*/
param richesse_initiale {PARCELLE};

/*
  L'engrais bonus utilisé à la première annéee qu'on calcule.
*/
var kilos_engrais_bonus_utilise {PARCELLE} >= 0;

/*
  La richesse la plus basse parmis toutes les parcelles qu'on calcule.
*/
var minimum_richesse_parcelle;

/*
  legume_est_affecte_parcelle[l, p, a] =
  - 1: le légume l est cultivé sur la parcelle p lors de l'année a
  - 0 sinon
*/
var legume_est_affecte_parcelle {LEGUME, PARCELLE, ANNEE} binary;

/*
  richesse_parcelle_annee[p, a]: la richesse d'une parcelle p à la fin de l'année a.
*/
var richesse_parcelle_annee {PARCELLE, ANNEE};

/*
  On maximise la richesse de la parcelle la plus pauvre. Voir la première contrainte.
*/
maximize minimum_richesse_parcelle_sixième_annee :
	 minimum_richesse_parcelle;


/*
  On calcule la richese de la parcelle la plus pauvre à l'année N_ANNEES (dernière année).
*/
subject to calcul_minimum_richesse_annee_six {p in PARCELLE}:
	minimum_richesse_parcelle <= richesse_parcelle_annee[p, N_ANNEES];


/*
  La quantité totale d'engrais bonus utilisé ne doit pas dépasser KILO_ENGRRAIS_BONUS_DISPO (quantité disponible orignalement)
*/
subject to engrais_max_premiere_annee :
	sum {p in PARCELLE}
    kilos_engrais_bonus_utilise[p] <= KILO_ENGRRAIS_BONUS_DISPO;


/*
  Calcul de la richesse des parcelles lors de la première année (avec utilisation d'engrais).
*/
subject to calcul_richesse_parcelle_avec_engrais { p in PARCELLE } :
	richesse_parcelle_annee[p,1] =
				     sum {l in LEGUME} (apport_besoin_net_an[l] * legume_est_affecte_parcelle[l,p,1]) + kilos_engrais_bonus_utilise[p] * 0.01
				     + richesse_initiale[p];

/*
  Calcul de la richesse des parcelles lors des autres années (sans utilisation d'engrais).
*/
subject to calcul_richesse_parcelle_sans_engrais { p in PARCELLE, a in 2..N_ANNEES } :
	richesse_parcelle_annee[p,a] =
				     sum {l in LEGUME}
                (apport_besoin_net_an[l] * legume_est_affecte_parcelle[l,p,a]) + richesse_parcelle_annee[p, a-1];


/*
  On verifie que la richesse de la parcelle est positive
*/
subject to richesse_positive {p in PARCELLE, a in ANNEE} :
	richesse_parcelle_annee[p, a] >= 0;


/*
  On s'assure qu'un seul legume est planté par parcelle lors d'une même année.
*/
subject to un_legume_par_parcelle {p in PARCELLE, a in ANNEE} :
	sum{l in LEGUME} legume_est_affecte_parcelle[l,p,a] = 1;


/*
  Chaque type de légume doit être cultivé au moins une fois par année.
*/
subject to chaque_type_legume_utilise_au_moins_une_fois {l in LEGUME, a in ANNEE} :
	sum {p in PARCELLE} legume_est_affecte_parcelle[l,p,a] >= 1;


/*
  Verifie, pour chaque legume et chaque parcelle, s'il est possible de les cultiver lors de la première année
*/
subject to peut_cultiver_legume_premiere_annee {p in PARCELLE, l in LEGUME} :
	richesse_initiale[p] + apport_besoin_net_an[l] * legume_est_affecte_parcelle[l,p,1] + kilos_engrais_bonus_utilise[p]*0.01 >= 0;


/*
  Verifie, pour chaque legume et chaque parcelle, s'il esy possible de les cultiver lors des autres années
*/
subject to peut_cultiver_legume_autres_annees {p in PARCELLE, l in LEGUME, a in 2..N_ANNEES} :
	legume_est_affecte_parcelle[next(l),p,a] = legume_est_affecte_parcelle[l,p,a-1];

data;

set LEGUME := fruits feuilles racines gousses engrais_verts;
set PARCELLE := A B C D E F G;

param :         prix_vente_euro_kg    apport_besoin_net_an      rendement_tonne_parcelle_an :=
fruits                4.3                  -20.2                          2.7
feuilles              1.2                  -10.1                          2.6
racines               2.7                   -5.5                          3.4
gousses               3.3                    3.5                          0.5
engrais_verts           0                   25.8                            0;

param : richesse_initiale :=
A						43.2
B						12.1
C						20.8
D						16.8
E						5.7
F						16.3
G						15.1;
